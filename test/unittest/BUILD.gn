# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/customization/enterprise_device_management/edm.gni")
import("//build/test.gni")

module_output_path = "enterprise_device_management/services"

config("module_private_config") {
  visibility = [ ":*" ]
  include_dirs = [ "$SUBSYSTEM_DIR/test" ]
}

config("edm_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "$EDM_SA_ROOT/include",
    "$EDM_INNERKITS_PATH/plugin_kits/include",
    "$EDM_INNERKITS_PATH/plugin_kits/include/utils",
    "$EDM_SA_ROOT/include/connection",
    JSONCPP_INCLUDE_DIR,
    "$SUBSYSTEM_DIR/common/native/include",
    "$SUBSYSTEM_DIR/framework/extension/include",
  ]
}

ohos_static_library("edmservice_static") {
  testonly = true
  sources = [
    "$EDM_SRC_PATH/ability_manager_death_recipient.cpp",
    "$EDM_SRC_PATH/admin.cpp",
    "$EDM_SRC_PATH/admin_manager.cpp",
    "$EDM_SRC_PATH/application_state_observer.cpp",
    "$EDM_SRC_PATH/connection/enterprise_admin_connection.cpp",
    "$EDM_SRC_PATH/connection/enterprise_admin_proxy.cpp",
    "$EDM_SRC_PATH/connection/enterprise_bundle_connection.cpp",
    "$EDM_SRC_PATH/connection/enterprise_conn_manager.cpp",
    "$EDM_SRC_PATH/enterprise_device_mgr_ability.cpp",
    "$EDM_SRC_PATH/enterprise_device_mgr_stub.cpp",
    "$EDM_SRC_PATH/permission_manager.cpp",
    "$EDM_SRC_PATH/plugin_manager.cpp",
    "$EDM_SRC_PATH/policy_manager.cpp",
    "$EDM_SRC_PATH/super_admin.cpp",
  ]

  public_configs = [
    ":edm_config",
    "$SUBSYSTEM_DIR/common/config:coverage_flags",
  ]

  defines = []

  if (target_cpu == "arm64") {
    defines += [ "_ARM64_" ]
    print("defines _ARM64_")
  }

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_manager",
    "ability_runtime:app_manager",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "ipc:ipc_core",
    "os_account:os_account_innerkits",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  if (is_standard_system) {
    external_deps += [ "hiviewdfx_hilog_native:libhilog" ]
  } else {
    external_deps += [ "hilog:libhilog" ]
  }

  deps = [
    "$EDM_INNERKITS_PATH:edmservice_kits",
    "$EDM_INNERKITS_PATH/plugin_kits:plugin_kits",
    "$SUBSYSTEM_DIR/common/native:edm_commom",
    "//third_party/jsoncpp:jsoncpp",
  ]

  subsystem_name = "customization"
  part_name = "enterprise_device_management"
}

ohos_unittest("EdmServicesUnitTest") {
  module_out_path = module_output_path

  include_dirs = [
    "//commonlibrary/c_utils/base/include",
    "//utils/system/safwk/native/include",
    "$EDM_SA_ROOT/include",
    "$EDM_SA_ROOT/include/utils",
    "//commonlibrary/c_utils/base:utils_config",
    JSONCPP_INCLUDE_DIR,
    "./include",
    "$SUBSYSTEM_DIR/interfaces/inner_api/include",
    "$SUBSYSTEM_DIR/common/native/include",
    "$SUBSYSTEM_DIR/services/edm_plugin/include",
    "$SUBSYSTEM_DIR/interfaces/inner_api/plugin_kits/include",
    "$SUBSYSTEM_DIR/interfaces/inner_api/plugin_kits/include/utils",
  ]

  sources = [
    "./src/admin_manager_test.cpp",
    "./src/cmd_utils.cpp",
    "./src/datetime_manager_proxy_test.cpp",
    "./src/edm_data_ability_utils_test.cpp",
    "./src/edm_permission_test.cpp",
    "./src/edm_sys_manager_test.cpp",
    "./src/ent_info_test.cpp",
    "./src/enterprise_admin_connection_test.cpp",
    "./src/enterprise_bundle_connection_test.cpp",
    "./src/enterprise_conn_manager_test.cpp",
    "./src/enterprise_device_mgr_ability_test.cpp",
    "./src/enterprise_device_mgr_proxy_test.cpp",
    "./src/iplugin_template_test.cpp",
    "./src/iplugin_test.cpp",
    "./src/permission_manager_test.cpp",
    "./src/plugin_manager_test.cpp",
    "./src/policy_manager_test.cpp",
    "./src/policy_serializer_test.cpp",
    "./src/utils_test.cpp",
  ]

  configs = [
    ":module_private_config",
    "//base/customization/enterprise_device_management/common/config:coverage_flags",
  ]

  deps = [
    ":edmservice_static",
    "$EDM_INNERKITS_PATH:edmservice_kits",
    "$SUBSYSTEM_DIR/common/native:edm_commom",
    "$SUBSYSTEM_DIR/interfaces/inner_api/plugin_kits:plugin_kits",
    "//third_party/googletest:gmock_main",
    "//third_party/googletest:gtest_main",
    "//third_party/jsoncpp:jsoncpp",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_manager",
    "access_token:libaccesstoken_sdk",
    "access_token:libnativetoken",
    "access_token:libtoken_setproc",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]

  if (is_standard_system) {
    external_deps += [ "hiviewdfx_hilog_native:libhilog" ]
  } else {
    external_deps += [ "hilog:libhilog" ]
  }

  subsystem_name = "customization"
  part_name = "enterprise_device_management"
}

group("unittest") {
  testonly = true
  deps = []

  deps += [
    # deps file
    ":EdmServicesUnitTest",
    "account_manager_proxy:unittest",
    "bundle_manager_proxy:unittest",
    "device_control_proxy:unittest",
    "device_info_proxy:unittest",
    "edm_plugin:unittest",
    "enterprise_device_mgr_ability:unittest",
    "enterprise_device_mgr_proxy:unittest",
    "network_manager_proxy:unittest",
    "wifi_manager_proxy:unittest",
  ]
}
