# Copyright (c) 2022-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("../../../../common/config/common.gni")

module_output_path = "enterprise_device_management/enterprise_device_management"

config("module_private_config") {
  visibility = [ ":*" ]
  include_dirs = [ "../" ]
}

config("edm_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "../../../../services/edm/include",
    "../../../../services/edm/include/ability",
    "../../../../services/edm/include/admin",
    "../../../../services/edm/include/connection",
    "../../../../services/edm/include/database",
    "../../../../services/edm/include/query_policy",
    "../../../../services/edm/include/strategy",
    "../../../../services/edm/include/watermark",
    "../../../../services/edm_plugin/include",
    "../../../../services/edm_plugin/include/utils",
    "../../../../framework/extension/include",
  ]
}

ohos_static_library("edmservice_static") {
  testonly = true
  sources = [
    "../../../../services/edm/src/ability/ability_controller.cpp",
    "../../../../services/edm/src/ability/ability_controller_factory.cpp",
    "../../../../services/edm/src/ability/app_service_extension_controller.cpp",
    "../../../../services/edm/src/ability/ui_ability_controller.cpp",
    "../../../../services/edm/src/admin/admin.cpp",
    "../../../../services/edm/src/admin/byod_admin.cpp",
    "../../../../services/edm/src/admin/device_admin.cpp",
    "../../../../services/edm/src/admin/sub_super_device_admin.cpp",
    "../../../../services/edm/src/admin/super_device_admin.cpp",
    "../../../../services/edm/src/admin/virtual_device_admin.cpp",
    "../../../../services/edm/src/admin_container.cpp",
    "../../../../services/edm/src/admin_manager.cpp",
    "../../../../services/edm/src/admin_policies_storage_rdb.cpp",
    "../../../../services/edm/src/application_state_observer.cpp",
    "../../../../services/edm/src/connection/enterprise_account_connection.cpp",
    "../../../../services/edm/src/connection/enterprise_admin_connection.cpp",
    "../../../../services/edm/src/connection/enterprise_admin_proxy.cpp",
    "../../../../services/edm/src/connection/enterprise_bundle_connection.cpp",
    "../../../../services/edm/src/connection/enterprise_conn_manager.cpp",
    "../../../../services/edm/src/connection/enterprise_kiosk_connection.cpp",
    "../../../../services/edm/src/connection/enterprise_market_connection.cpp",
    "../../../../services/edm/src/connection/enterprise_update_connection.cpp",
    "../../../../services/edm/src/database/edm_rdb_data_manager.cpp",
    "../../../../services/edm/src/database/edm_rdb_open_callback.cpp",
    "../../../../services/edm/src/device_policies_storage_rdb.cpp",
    "../../../../services/edm/src/enterprise_device_mgr_ability.cpp",
    "../../../../services/edm/src/enterprise_device_mgr_stub.cpp",
    "../../../../services/edm/src/hisysevent_adapter.cpp",
    "../../../../services/edm/src/language_manager.cpp",
    "../../../../services/edm/src/permission_checker.cpp",
    "../../../../services/edm/src/plugin_manager.cpp",
    "../../../../services/edm/src/policy_manager.cpp",
    "../../../../services/edm/src/query_policy/allowed_app_distribution_types_query.cpp",
    "../../../../services/edm/src/query_policy/allowed_install_bundles_query.cpp",
    "../../../../services/edm/src/query_policy/disable_app_clone_query.cpp",
    "../../../../services/edm/src/query_policy/disable_backup_and_restore_query.cpp",
    "../../../../services/edm/src/query_policy/disallow_external_storage_card_query.cpp",
    "../../../../services/edm/src/query_policy/disable_set_biometrics_and_screenLock_query.cpp",
    "../../../../services/edm/src/query_policy/disable_set_device_name_query.cpp",
    "../../../../services/edm/src/query_policy/disallow_export_recovery_key_query.cpp",
    "../../../../services/edm/src/query_policy/disallow_open_file_boost_query.cpp",
    "../../../../services/edm/src/query_policy/disallow_distributed_transmission_query.cpp",
    "../../../../services/edm/src/query_policy/disallow_modify_apn_query.cpp",
    "../../../../services/edm/src/query_policy/disallow_modify_datetime_query.cpp",
    "../../../../services/edm/src/query_policy/disallow_unmute_device_query.cpp",
    "../../../../services/edm/src/query_policy/disallow_virtual_service_query.cpp",
    "../../../../services/edm/src/query_policy/disallowed_install_bundles_query.cpp",
    "../../../../services/edm/src/query_policy/disallowed_tethering_query.cpp",
    "../../../../services/edm/src/query_policy/disallowed_uninstall_bundles_query.cpp",
    "../../../../services/edm/src/query_policy/get_auto_unlock_after_reboot_query.cpp",
    "../../../../services/edm/src/query_policy/get_device_encryption_status_query.cpp",
    "../../../../services/edm/src/query_policy/get_display_version_query.cpp",
    "../../../../services/edm/src/query_policy/get_security_patch_tag_query.cpp",
    "../../../../services/edm/src/query_policy/inactive_user_freeze_query.cpp",
    "../../../../services/edm/src/query_policy/installed_bundle_info_list_query.cpp",
    "../../../../services/edm/src/query_policy/ipolicy_query.cpp",
    "../../../../services/edm/src/query_policy/is_app_kiosk_allowed_query.cpp",
    "../../../../services/edm/src/query_policy/ntp_server_query.cpp",
    "../../../../services/edm/src/query_policy/password_policy_query.cpp",
    "../../../../services/edm/src/query_policy/plugin_policy_reader.cpp",
    "../../../../services/edm/src/query_policy/snapshot_skip_query.cpp",
    "../../../../services/edm/src/strategy/enhance_execute_strategy.cpp",
    "../../../../services/edm/src/strategy/replace_execute_strategy.cpp",
    "../../../../services/edm/src/strategy/single_execute_strategy.cpp",
    "../../../../services/edm/src/user_policy_manager.cpp",
    "../../../../services/edm_plugin/src/utils/usb_policy_utils.cpp",
    "../../../../services/edm/src/query_policy/disallowed_sms_query.cpp",
    "../../../../services/edm/src/query_policy/disallowed_mms_query.cpp",
    "../../../../services/edm_plugin/src/set_browser_policies_plugin.cpp",
  ]

  public_configs = [
    ":edm_config",
    "../../../../common/config:coverage_flags",
  ]

  defines = [
    "USERIAM_EDM_ENABLE",
    "EDM_SUPPORT_ALL_ENABLE"
  ]

  if (target_cpu == "arm64") {
    defines += [ "_ARM64_" ]
    print("defines _ARM64_")
  }

  if (target_cpu == "x86_64") {
    defines += [ "_X86_64_" ]
    print("defines _X86_64_")
  }

  if (wifi_edm_enable) {
    sources += [
      "../../../../services/edm/src/query_policy/allowed_wifi_list_query.cpp",
      "../../../../services/edm/src/query_policy/disallowed_wifi_list_query.cpp",
      "../../../../services/edm/src/query_policy/set_wifi_disabled_query.cpp",
      "../../../../services/edm/src/query_policy/disallow_random_mac_address_query.cpp",
    ]
  }

  if (drivers_interface_usb_edm_enable && storage_service_edm_enable &&
      usb_manager_edm_enable) {
    sources +=
        [ "../../../../services/edm/src/query_policy/usb_read_only_query.cpp" ]
  }

  if (location_edm_enable) {
    sources += [
      "../../../../services/edm/src/query_policy/location_policy_query.cpp",
    ]
  }

  if (useriam_edm_enable) {
    sources += [
      "../../../../services/edm/src/query_policy/fingerprint_auth_query.cpp",
    ]
  }

  if (ability_runtime_edm_enable) {
    sources += [
      "../../../../services/edm/src/query_policy/allowed_running_bundles_query.cpp",
      "../../../../services/edm/src/query_policy/disallowed_running_bundles_query.cpp"
    ]
  }

  if (bluetooth_edm_enable) {
    sources += [
      "../../../../services/edm/src/query_policy/allowed_bluetooth_devices_query.cpp",
      "../../../../services/edm/src/query_policy/disable_bluetooth_query.cpp",
      "../../../../services/edm/src/query_policy/disallowed_bluetooth_devices_query.cpp",
    ]
    defines += ["BLUETOOTH_EDM_ENABLE"]
  }

  if (camera_framework_edm_enable) {
    sources +=
        [ "../../../../services/edm/src/query_policy/disable_camera_query.cpp" ]
  }

  if (audio_framework_edm_enable) {
    sources += [
      "../../../../services/edm/src/query_policy/disable_hdc_query.cpp",
      "../../../../services/edm/src/query_policy/disable_microphone_query.cpp",
      "../../../../services/edm/src/query_policy/disable_printer_query.cpp",
    ]
  }

  if (drivers_interface_usb_edm_enable && usb_manager_edm_enable) {
    sources += [
      "../../../../services/edm/src/query_policy/allowed_usb_devices_query.cpp",
      "../../../../services/edm/src/query_policy/disable_usb_query.cpp",
    ]
    defines += ["USB_SERVICE_EDM_ENABLE"]
  }

  if (pasteboard_edm_enable) {
    sources += [
      "../../../../services/edm/src/query_policy/clipboard_policy_query.cpp",
    ]
  }

  sources += [
      "../../../../services/edm/src/query_policy/permission_managed_state_query.cpp",
    ]

  if (common_event_service_edm_enable) {
    defines += [ "COMMON_EVENT_SERVICE_EDM_ENABLE" ]
    sources += [ "../../../../services/edm/src/query_policy/set_browser_policies_query.cpp" ]
  }

  if (power_manager_edm_enable && !enterprise_device_management_feature_pc_only) {
    sources += [ "../../../../services/edm/src/query_policy/disallow_power_long_press_query.cpp" ]
  }

  if (netmanager_base_edm_enable) {
    sources += [ "../../../../services/edm/src/query_policy/disallowed_airplane_mode_query.cpp" ]
  }

  if (notification_edm_enable) {
    sources += [
      "../../../../services/edm/src/query_policy/disallowed_notification_query.cpp",
    ]
  }

  external_deps = [
    "ability_base:want",
    "ability_runtime:app_manager",
    "ability_runtime:extension_manager",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "bundle_framework:appexecfwk_base",
    "cJSON:cjson",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "file_api:securitylabel",
    "graphic_2d:librender_service_client",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "i18n:intl_util",
    "init:libbegetutil",
    "ipc:ipc_core",
    "pasteboard:pasteboard_client",
    "relational_store:native_rdb",
    "resource_management:global_resmgr",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "window_manager:libwm",
    "window_manager:sms",
  ]

  if (os_account_edm_enable) {
    external_deps += [
      "os_account:libaccountkits",
      "os_account:os_account_innerkits",
    ]

    sources += [
      "../../../../services/edm/src/query_policy/disable_maintenance_mode_query.cpp",
      "../../../../services/edm/src/query_policy/disable_mtp_client_query.cpp",
      "../../../../services/edm/src/query_policy/disable_mtp_server_query.cpp",
      "../../../../services/edm/src/query_policy/disable_user_mtp_client_query.cpp",
      "../../../../services/edm/src/query_policy/disable_samba_client_query.cpp",
      "../../../../services/edm/src/query_policy/disable_samba_server_query.cpp",
      "../../../../services/edm/src/query_policy/disallow_add_local_account_query.cpp",
      "../../../../services/edm/src/watermark/watermark_application_observer.cpp",
      "../../../../services/edm/src/watermark/watermark_observer_manager.cpp",
      "../../../../services/edm/src/query_policy/disallow_modify_wallpaper_query.cpp",
    ]
    defines += [ "OS_ACCOUNT_EDM_ENABLE" ]
  }

  if (drivers_interface_usb_edm_enable && usb_manager_edm_enable) {
    external_deps += [ "usb_manager:usbsrv_client" ]
  }

  if (os_account_edm_enable && enterprise_device_management_feature_pc_only) {
    external_deps += [
      "os_account:libaccountkits",
      "os_account:os_account_innerkits",
    ]

    sources += [
      "../../../../services/edm/src/query_policy/disable_print_query.cpp",
      "../../../../services/edm/src/query_policy/disable_sudo_query.cpp",
    ]
    defines += [
      "FEATURE_PC_ONLY",
      "SUDO_EDM_ENABLE"
    ]
  }

  if (enterprise_device_management_feature_pc_only) {
      sources += [
        "../../../../services/edm/src/query_policy/disable_usb_storage_device_write_query.cpp",
        "../../../../services/edm/src/query_policy/install_local_enterprise_app_enabled_query.cpp",
        "../../../../services/edm/src/query_policy/disable_hdc_remote_query.cpp",
        "../../../../services/edm/src/query_policy/disable_running_binary_app_query.cpp",
      ]
      defines += [ "FEATURE_PC_ONLY" ]
  }

  if (netmanager_ext_edm_enable) {
    sources += [
      "../../../../services/edm/src/query_policy/disallow_vpn_query.cpp",
    ]
  }

  deps = [
    "../../../../common/external:edm_external_adapters",
    "../../../../common/native:edm_commom",
    "../../../../interfaces/inner_api:edmservice_kits",
    "../../../../interfaces/inner_api/plugin_kits:plugin_kits",
    "../../../../services/idl:enterprise_device_mgr_idl_stub",
  ]

  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
  }
  branch_protector_ret = "pac_ret"
  subsystem_name = "customization"
  part_name = "enterprise_device_management"
}

ohos_unittest("EdmServicesUnitTest") {
  module_out_path = module_output_path

  include_dirs = [
    "./include",
    "./include/mock",
    "../../common/mock/include",
    "../../external_mock/include",
    "../../../../interfaces/inner_api/plugin_kits/include/utils",
  ]

  sources = [
    "./src/ability_controller_factory_test.cpp",
    "./src/admin_container_test.cpp",
    "./src/admin_manager_test.cpp",
    "./src/admin_test.cpp",
    "./src/device_policies_storage_rdb_test.cpp",
    "./src/enhance_execute_strategy_test.cpp",
    "./src/enterprise_account_connection_test.cpp",
    "./src/enterprise_admin_connection_test.cpp",
    "./src/enterprise_bundle_connection_test.cpp",
    "./src/enterprise_conn_manager_test.cpp",
    "./src/enterprise_device_mgr_ability_test.cpp",
    "./src/enterprise_kiosk_connection_test.cpp",
    "./src/enterprise_update_connection_test.cpp",
    "./src/installed_bundle_info_list_query_test.cpp",
    "./src/iplugin_template_test.cpp",
    "./src/language_manager_test.cpp",
    "./src/permission_checker_test.cpp",
    "./src/plugin_manager_test.cpp",
    "./src/plugin_policy_query_test.cpp",
    "./src/policy_manager_test.cpp",
    "./src/replace_execute_strategy_test.cpp",
    "./src/single_execute_strategy_test.cpp",
    "./src/user_policy_manager_test.cpp",
  ]

  defines = []
  if (common_event_service_edm_enable) {
    defines += [ "COMMON_EVENT_SERVICE_EDM_ENABLE" ]
  }

  if (enterprise_device_management_feature_pc_only){
    defines += [ "FEATURE_PC_ONLY" ]
  }

  if (!enterprise_device_management_feature_pc_only) {
    sources += [ "../../../../services/edm/src/query_policy/disable_private_space_query.cpp" ]
    defines += [
      "BACKUP_AND_RESTORE_EDM_ENABLE",
      "PRIVATE_SPACE_EDM_ENABLE"
    ]
  }

  if (enterprise_device_management_feature_pc_only) {
    sources += [ "../../../../services/edm/src/query_policy/disallow_modify_ethernet_ip_query.cpp" ]
    defines += [ "FEATURE_PC_ONLY" ]
  }

  if (notification_edm_enable) {
    defines += [ "NOTIFICATION_EDM_ENABLE" ]
  }

  if (netmanager_ext_edm_enable) {
    defines += [ "NETMANAGER_EXT_EDM_ENABLE" ]
  }

  if (ability_runtime_edm_enable) {
    defines += [ "ABILITY_RUNTIME_EDM_ENABLE" ]
  }

  configs = [
    ":module_private_config",
    "../../../../common/config:coverage_flags",
  ]

  deps = [
    ":edmservice_static",
    "../../../../common/external:edm_external_adapters",
    "../../../../common/native:edm_commom",
    "../../../../interfaces/inner_api:edmservice_kits",
    "../../../../interfaces/inner_api/plugin_kits:plugin_kits",
    "../../utils:edm_unittest_utils",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_connect_callback_stub",
    "ability_runtime:ability_manager",
    "access_token:libaccesstoken_sdk",
    "access_token:libnativetoken",
    "access_token:libnativetoken_shared",
    "access_token:libtoken_setproc",
    "bundle_framework:appexecfwk_core",
    "cJSON:cjson",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "file_api:securitylabel",
    "googletest:gmock_main",
    "hilog:libhilog",
    "i18n:intl_util",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "window_manager:sms",
  ]

  if (os_account_edm_enable) {
    external_deps += [
      "os_account:libaccountkits",
      "os_account:os_account_innerkits",
    ]
    defines += [ "OS_ACCOUNT_EDM_ENABLE" ]
  }

  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
    blocklist = "../../cfi_blocklist.txt"
  }
  branch_protector_ret = "pac_ret"
  subsystem_name = "customization"
  part_name = "enterprise_device_management"
}

group("unittest") {
  testonly = true

  deps = [
    # deps file
    ":EdmServicesUnitTest",
  ]
}
