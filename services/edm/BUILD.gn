# Copyright (c) 2022-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../common/config/common.gni")

config("edm_config") {
  cflags = [ "-Wno-error=implicit-fallthrough" ]
  include_dirs = [
    "include",
    "include/connection",
    "include/database",
    "include/query_policy",
    "include/strategy",
    "include/watermark",
    "../../framework/extension/include",
  ]
}

ohos_shared_library("edmservice") {
  sources = [ "./src/enterprise_device_mgr_stub.cpp" ]
  public_configs = [
    ":edm_config",
    "../idl:enterprise_device_mgr_idl_gen_config",
    "../../common/config:coverage_flags",
  ]

  defines = []

  if (target_cpu == "arm64") {
    defines += [ "_ARM64_" ]
    print("defines _ARM64_")
  }

  if (target_cpu == "x86_64") {
    defines += [ "_X86_64_" ]
    print("defines _X86_64_")
  }

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_connect_callback_stub",
    "ability_runtime:app_manager",
    "ability_runtime:extension_manager",
    "ability_runtime:wantagent_innerkits",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "cJSON:cjson",
    "c_utils:utils",
    "file_api:securitylabel",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "jsoncpp:jsoncpp",
    "relational_store:native_rdb",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "window_manager:libwm",
  ]

  if (enterprise_device_management_support_all) {
    defines += [ "EDM_SUPPORT_ALL_ENABLE" ]

    sources += [
      "./src/admin.cpp",
      "./src/admin_container.cpp",
      "./src/admin_manager.cpp",
      "./src/admin_policies_storage_rdb.cpp",
      "./src/application_state_observer.cpp",
      "./src/connection/enterprise_account_connection.cpp",
      "./src/connection/enterprise_admin_connection.cpp",
      "./src/connection/enterprise_admin_proxy.cpp",
      "./src/connection/enterprise_bundle_connection.cpp",
      "./src/connection/enterprise_conn_manager.cpp",
      "./src/connection/enterprise_update_connection.cpp",
      "./src/database/edm_rdb_data_manager.cpp",
      "./src/database/edm_rdb_open_callback.cpp",
      "./src/device_policies_storage_rdb.cpp",
      "./src/enterprise_device_mgr_ability.cpp",
      "./src/permission_checker.cpp",
      "./src/permission_manager.cpp",
      "./src/plugin_manager.cpp",
      "./src/policy_manager.cpp",
      "./src/query_policy/allowed_install_bundles_query.cpp",
      "./src/query_policy/disable_maintenance_mode_query.cpp",
      "./src/query_policy/disable_mtp_client_query.cpp",
      "./src/query_policy/disable_mtp_server_query.cpp",
      "./src/query_policy/disable_remote_desk_query.cpp",
      "./src/query_policy/disable_remote_diagnosis_query.cpp",
      "./src/query_policy/disable_user_mtp_client_query.cpp",
      "./src/query_policy/disallow_modify_datetime_query.cpp",
      "./src/query_policy/disallowed_install_bundles_query.cpp",
      "./src/query_policy/disallowed_tethering_query.cpp",
      "./src/query_policy/disallowed_uninstall_bundles_query.cpp",
      "./src/query_policy/get_device_encryption_status_query.cpp",
      "./src/query_policy/get_display_version_query.cpp",
      "./src/query_policy/get_security_patch_tag_query.cpp",
      "./src/query_policy/inactive_user_freeze_query.cpp",
      "./src/query_policy/installed_bundle_info_list_query.cpp",
      "./src/query_policy/ipolicy_query.cpp",
      "./src/query_policy/ntp_server_query.cpp",
      "./src/query_policy/plugin_policy_reader.cpp",
      "./src/query_policy/snapshot_skip_query.cpp",
      "./src/strategy/enhance_execute_strategy.cpp",
      "./src/strategy/replace_execute_strategy.cpp",
      "./src/strategy/single_execute_strategy.cpp",
      "./src/user_policy_manager.cpp",
    ]
  }

  if (wifi_edm_enable) {
    sources += [
      "./src/query_policy/allowed_wifi_list_query.cpp",
      "./src/query_policy/disallowed_wifi_list_query.cpp",
      "./src/query_policy/set_wifi_disabled_query.cpp",
    ]
    defines += [ "WIFI_EDM_ENABLE" ]
  }

  if (location_edm_enable) {
    sources += [ "./src/query_policy/location_policy_query.cpp" ]
    defines += [ "LOCATION_EDM_ENABLE" ]
  }

  if (useriam_edm_enable) {
    sources += [
      "./src/query_policy/fingerprint_auth_query.cpp",
      "./src/query_policy/password_policy_query.cpp",
    ]
    defines += [ "USERIAM_EDM_ENABLE" ]
  }

  if (enterprise_device_management_feature_pc_only) {
    sources += [
      "./src/query_policy/disable_samba_client_query.cpp",
      "./src/query_policy/disable_samba_server_query.cpp",
    ]
    defines += [ "SAMBA_EDM_ENABLE" ]
  }

  if (ability_runtime_edm_enable) {
    sources += [ "./src/query_policy/disallowed_running_bundles_query.cpp" ]
    defines += [ "ABILITY_RUNTIME_EDM_ENABLE" ]
  }

  if (bluetooth_edm_enable) {
    sources += [
      "./src/query_policy/allowed_bluetooth_devices_query.cpp",
      "./src/query_policy/disable_bluetooth_query.cpp",
      "./src/query_policy/disallowed_bluetooth_devices_query.cpp",
    ]
    defines += [ "BLUETOOTH_EDM_ENABLE" ]
  }

  if (camera_framework_edm_enable) {
    sources += [ "./src/query_policy/disable_camera_query.cpp" ]
    defines += [ "CAMERA_FRAMEWORK_EDM_ENABLE" ]
  }

  if (audio_framework_edm_enable) {
    sources += [
      "./src/query_policy/disable_hdc_query.cpp",
      "./src/query_policy/disable_microphone_query.cpp",
      "./src/query_policy/disable_printer_query.cpp",
    ]
    defines += [ "AUDIO_FRAMEWORK_EDM_ENABLE" ]
  }

  if (drivers_interface_usb_edm_enable && usb_manager_edm_enable) {
    sources += [
      "./src/query_policy/allowed_usb_devices_query.cpp",
      "./src/query_policy/disable_usb_query.cpp",
    ]
    defines += [ "USB_SERVICE_EDM_ENABLE" ]
  }

  if (drivers_interface_usb_edm_enable && storage_service_edm_enable &&
      usb_manager_edm_enable) {
    sources += [ "./src/query_policy/usb_read_only_query.cpp" ]
    defines += [ "USB_STORAGE_SERVICE_EDM_ENABLE" ]
  }

  if (pasteboard_edm_enable) {
    sources += [ "./src/query_policy/clipboard_policy_query.cpp" ]
    defines += [ "PASTEBOARD_EDM_ENABLE" ]
  }

  if (os_account_edm_enable) {
    external_deps += [
      "os_account:libaccountkits",
      "os_account:os_account_innerkits",
    ]

    sources += [
      "./src/query_policy/disallow_add_local_account_query.cpp",
      "./src/watermark/watermark_application_observer.cpp",
      "./src/watermark/watermark_observer_manager.cpp",
    ]
    defines += [ "OS_ACCOUNT_EDM_ENABLE" ]
  }

  if (netmanager_base_edm_enable) {
    defines += [ "NET_MANAGER_BASE_EDM_ENABLE" ]
  }

  if (common_event_service_edm_enable) {
    external_deps += [ "common_event_service:cesfwk_innerkits" ]
    sources += [ "./src/query_policy/set_browser_policies_query.cpp" ]
    defines += [ "COMMON_EVENT_SERVICE_EDM_ENABLE" ]
  }

  if (nfc_edm_enable) {
    sources += [
      "./src/query_policy/disallowed_nfc_query.cpp",
    ]
    defines += [ "NFC_EDM_ENABLE" ]
  }

  if (!enterprise_device_management_feature_pc_only && sms_mms_edm_enable) {
    sources += [ 
      "./src/query_policy/disallowed_sms_query.cpp",
      "./src/query_policy/disallowed_mms_query.cpp",
    ]
    defines += [ 
      "SMS_EDM_ENABLE",
      "MMS_EDM_ENABLE"
    ]
  }

  if (!enterprise_device_management_feature_pc_only) {
    sources += [ "./src/query_policy/disable_backup_and_restore_query.cpp" ]
    defines += [ "BACKUP_AND_RESTORE_EDM_ENABLE" ]
  }

  if (!enterprise_device_management_feature_pc_only &&
      cellular_data_edm_enable) {
    sources += [
      "./src/query_policy/disallow_modify_apn_query.cpp",
      "./src/query_policy/disallowed_mobile_data_query.cpp",
    ]
    defines += [
      "APN_EDM_ENABLE",
      "MOBILE_DATA_ENABLE",
    ]
  }

  if (!enterprise_device_management_feature_pc_only && power_manager_edm_enable) {
    sources += [ "./src/query_policy/disallow_power_long_press_query.cpp" ]
    defines += [ "POWER_MANAGER_EDM_ENABLE" ]
  }

  deps = [
    "../../common/external:edm_external_adapters",
    "../../common/native:edm_commom",
    "../../interfaces/inner_api:edmservice_kits",
    "../../interfaces/inner_api/plugin_kits:plugin_kits",
    "../idl:enterprise_device_mgr_idl_stub",
  ]

  shlib_type = "sa"

  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
  }
  branch_protector_ret = "pac_ret"
  subsystem_name = "customization"
  part_name = "enterprise_device_management"
}
