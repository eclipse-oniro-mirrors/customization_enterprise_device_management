# Copyright (c) 2022-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../common/config/common.gni")

config("edm_plugin_config") {
  cflags = [
    "-Wno-error=implicit-fallthrough",
    "-fvisibility=hidden",
  ]
  include_dirs = [
    "./include",
    "./include/network",
    "./include/network/executer",
    "./include/network/rule",
    "./include/utils",
  ]
}

ohos_shared_library("device_core_plugin") {
  sources = [
    "./src/utils/installer_callback.cpp",
    "./src/utils/uninstall_param_serializer.cpp",
  ]

  public_configs = [
    ":edm_plugin_config",
    "../../common/config:coverage_flags",
  ]

  defines = []

  if (target_cpu == "arm64") {
    defines += [ "_ARM64_" ]
    print("defines _ARM64_")
  }

  if (target_cpu == "x86_64") {
    defines += [ "_X86_64_" ]
    print("defines _X86_64_")
  }

  external_deps = [
    "ability_base:want",
    "ability_runtime:app_manager",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "cJSON:cjson",
    "c_utils:utils",
    "distributed_notification_service:ans_innerkits",
    "file_api:securitylabel",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
    "window_manager:libwm",
    "window_manager:session_manager_lite",
    "window_manager:sms",
  ]

  if (enterprise_device_management_support_all) {
    defines += [ "EDM_SUPPORT_ALL_ENABLE" ]
    sources += [
      "./src/allowed_app_distribution_types_plugin.cpp",
      "./src/allowed_install_bundles_plugin.cpp",
      "./src/bundle_install_plugin.cpp",
      "./src/disable_hdc_plugin.cpp",
      "./src/disable_maintenance_mode_plugin.cpp",
      "./src/disable_mtp_server_plugin.cpp",
      "./src/disable_printer_plugin.cpp",
      "./src/disallow_modify_datetime_plugin.cpp",
      "./src/disable_set_biometrics_and_screenLock_plugin.cpp",
      "./src/disable_set_device_name_plugin.cpp",
      "./src/disallow_distributed_transmission_plugin.cpp",
      "./src/disallowed_install_bundles_plugin.cpp",
      "./src/disallowed_uninstall_bundles_plugin.cpp",
      "./src/disallow_external_storage_card_plugin.cpp",
      "./src/inactive_user_freeze_plugin.cpp",
      "./src/install_plugin.cpp",
      "./src/installed_bundle_info_list_plugin.cpp",
      "./src/kiosk_feature_plugin.cpp",
      "./src/ntp_server_plugin.cpp",
      "./src/power_policy_plugin.cpp",
      "./src/screen_off_time_plugin.cpp",
      "./src/snapshot_skip_plugin.cpp",
      "./src/uninstall_plugin.cpp",
      "./src/utils/allowed_app_distribution_types_utils.cpp",
      "./src/utils/battery_utils.cpp",
      "./src/utils/bluetooth_config_utils.cpp",
      "./src/set_allowed_kiosk_apps_plugin.cpp",
    ]

    if (enterprise_device_management_feature_pc_only) {
      sources += [
      "./src/disallow_export_recovery_key_plugin.cpp",
      "./src/disallowed_nearlink_protocols_plugin.cpp",
      "./src/utils/nearlink_config_utils.cpp",
      "./src/utils/nearlink_protocol_utils.cpp",
      ]
    }
    
    if (os_account_edm_enable) {
      external_deps += [
        "graphic_2d:librender_service_base",
        "graphic_2d:librender_service_client",
        "os_account:libaccountkits",
        "os_account:os_account_innerkits",
      ]
      sources += [
        "./src/add_os_account_plugin.cpp",
        "./src/disable_mtp_client_plugin.cpp",
        "./src/disable_user_mtp_client_plugin.cpp",
        "./src/disallow_add_local_account_plugin.cpp",
        "./src/disallow_add_os_account_by_user_plugin.cpp",
        "./src/set_watermark_image_plugin.cpp",
      ]
      defines += [ "OS_ACCOUNT_EDM_ENABLE" ]
    }

    if (os_account_edm_enable && enterprise_device_management_feature_pc_only) {
      external_deps += [ "os_account:domain_account_innerkits" ]
      sources += [
        "./src/set_domain_account_policy_plugin.cpp",
        "./src/disable_print_plugin.cpp",
        "./src/disable_sudo_plugin.cpp",
        "./src/disallowed_usb_storage_device_write_plugin.cpp",
        "./src/utils/array_usb_device_type_serializer.cpp",
      ]
      defines += [
        "SUDO_EDM_ENABLE",
        "FEATURE_PC_ONLY"
      ]
      external_deps += [ "usb_manager:usbsrv_client" ]
    }

    if (enterprise_device_management_feature_pc_only) {
      sources += [
        "./src/set_auto_unlock_after_reboot_plugin.cpp",
        "./src/install_local_enterprise_app_enabled_plugin.cpp",
      ]
    }

    if (audio_framework_edm_enable) {
      external_deps += [ "audio_framework:audio_client" ]
      sources += [ "./src/disable_microphone_plugin.cpp" ]
    }

    if (bluetooth_edm_enable) {
      external_deps += [ "bluetooth:btframework" ]
      sources += [
        "./src/allowed_bluetooth_devices_plugin.cpp",
        "./src/disable_bluetooth_plugin.cpp",
        "./src/disallowed_bluetooth_devices_plugin.cpp",
        "./src/disallowed_bluetooth_protocols_plugin.cpp",
        "./src/get_bluetooth_info_plugin.cpp",
        "./src/switch_bluetooth_plugin.cpp",
        "./src/utils/bt_protocol_utils.cpp",
      ]
    }

    if (camera_framework_edm_enable) {
      external_deps += [ "camera_framework:camera_framework" ]
      sources += [ "./src/disable_camera_plugin.cpp" ]
    }

    if (power_manager_edm_enable) {
      external_deps += [ "power_manager:powermgr_client" ]
      sources += [
        "./src/reboot_plugin.cpp",
        "./src/shutdown_plugin.cpp",
      ]
    }

    if (screenlock_mgr_edm_enable) {
      external_deps += [ "screenlock_mgr:screenlock_client" ]
      sources += [ "./src/lock_screen_plugin.cpp" ]
    }

    if (!enterprise_device_management_feature_pc_only) {
      sources += [
        "./src/disable_backup_and_restore_plugin.cpp",
        "./src/disable_private_space_plugin.cpp",
      ]
    }

    if (enterprise_device_management_feature_charging_type_setting) {
      defines += [ "FEATURE_CHARGING_TYPE_SETTING" ]
    }

    if (power_manager_edm_enable && !enterprise_device_management_feature_pc_only) {
      sources += [ "./src/disallow_power_long_press_plugin.cpp" ]
    }
  }

  deps = [
    "../../common/external:edm_external_adapters",
    "../../common/native:edm_commom",
    "../../interfaces/inner_api:edmservice_kits",
    "../../interfaces/inner_api/plugin_kits:plugin_kits",
  ]

  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
  }
  relative_install_dir = "edm_plugin"
  branch_protector_ret = "pac_ret"
  subsystem_name = "customization"
  part_name = "enterprise_device_management"
}

ohos_shared_library("communication_plugin") {
  sources = []

  public_configs = [
    ":edm_plugin_config",
    "../../common/config:coverage_flags",
  ]

  defines = []

  if (target_cpu == "arm64") {
    defines += [ "_ARM64_" ]
    print("defines _ARM64_")
  }

  if (target_cpu == "x86_64") {
    defines += [ "_X86_64_" ]
    print("defines _X86_64_")
  }

  external_deps = [
    "ability_base:want",
    "ability_runtime:app_manager",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "cJSON:cjson",
    "c_utils:utils",
    "file_api:securitylabel",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
    "window_manager:libwm",
    "window_manager:sms",
  ]

  if (enterprise_device_management_support_all) {
    sources += [ "./src/disallowed_tethering_plugin.cpp" ]
    defines += [ "EDM_SUPPORT_ALL_ENABLE" ]
    if (wifi_edm_enable) {
      external_deps += [ "wifi:wifi_sdk" ]
      sources += [
        "./src/allowed_wifi_list_plugin.cpp",
        "./src/disallowed_wifi_list_plugin.cpp",
        "./src/is_wifi_active_plugin.cpp",
        "./src/set_wifi_disabled_plugin.cpp",
        "./src/set_wifi_profile_plugin.cpp",
        "./src/switch_wifi_plugin.cpp",
        "./src/utils/wifi_device_config_serializer.cpp",
        "./src/utils/wifi_policy_utils.cpp",
      ]
      defines += [ "WIFI_EDM_ENABLE" ]
    }

    if (!enterprise_device_management_feature_pc_only &&
        cellular_data_edm_enable && telephony_core_edm_enable) {
      external_deps += [
        "call_manager:tel_call_manager_api",
        "cellular_data:tel_cellular_data_api",
        "core_service:tel_core_service_api",
        "data_share:datashare_consumer",
        "telephony_data:tel_telephony_data",
      ]
      sources += [
        "./src/disallow_mobile_data_plugin.cpp",
        "./src/disallow_modify_apn_plugin.cpp",
        "./src/disallowed_sim_plugin.cpp",
        "./src/disallowed_telephony_call_plugin.cpp",
        "./src/telephony_call_policy_plugin.cpp",
        "./src/set_apn_plugin.cpp",
        "./src/turnonoff_mobile_data_plugin.cpp",
        "./src/utils/apn_utils.cpp",
      ]
      defines += [
        "CELLULAR_DATA_EDM_ENABLE",
        "TELEPHONY_EDM_ENABLE",
      ]
    }

    if (enterprise_device_management_feature_pc_only) {
      sources += [
        "./src/disable_samba_client_plugin.cpp",
        "./src/disable_samba_server_plugin.cpp",
        "./src/disallow_modify_ethernet_ip_plugin.cpp",
      ]
      defines += [ "FEATURE_PC_ONLY" ]
    }

    if (netmanager_base_edm_enable) {
      defines += [ "NET_MANAGER_BASE_EDM_ENABLE" ]
      external_deps += [
        "netmanager_base:net_conn_manager_if",
        "netmanager_base:net_policy_manager_if",
        "netmanager_base:netsys_controller",
      ]
      sources += [
        "./src/disabled_network_interface_plugin.cpp",
        "./src/disallowed_airplane_mode_plugin.cpp",
        "./src/domain_filter_rule_plugin.cpp",
        "./src/firewall_rule_plugin.cpp",
        "./src/global_proxy_plugin.cpp",
        "./src/iptables_rule_plugin.cpp",
        "./src/network/executer/domain_executer.cpp",
        "./src/network/executer/executer_factory.cpp",
        "./src/network/executer/executer_utils.cpp",
        "./src/network/executer/firewall_executer.cpp",
        "./src/network/executer/iexecuter.cpp",
        "./src/network/iptables_manager.cpp",
        "./src/network/rule/chain_rule.cpp",
        "./src/network/rule/domain_chain_rule.cpp",
        "./src/network/rule/firewall_chain_rule.cpp",
        "./src/network/rule/rule_utils.cpp",
        "./src/utils/domain_filter_rule_serializer.cpp",
        "./src/utils/firewall_rule_serializer.cpp",
        "./src/utils/http_proxy_serializer.cpp",
      ]
    }

    if (netmanager_ext_edm_enable) {
      external_deps += [
        "netmanager_ext:ethernet_manager_if",
        "netmanager_ext:net_vpn_manager_if",
      ]
      sources += [
        "./src/get_all_network_interfaces_plugin.cpp",
        "./src/get_ip_address_plugin.cpp",
        "./src/get_mac_plugin.cpp",
        "./src/disallow_vpn_plugin.cpp"
      ]
    }

    if (drivers_interface_usb_edm_enable && usb_manager_edm_enable) {
      external_deps += [ "usb_manager:usbsrv_client" ]
      sources += [
        "./src/allowed_usb_devices_plugin.cpp",
        "./src/disable_usb_plugin.cpp",
        "./src/disallowed_usb_devices_plugin.cpp",
        "./src/utils/array_usb_device_type_serializer.cpp",
        "./src/utils/usb_policy_utils.cpp",
      ]
    }

    if (os_account_edm_enable) {
      external_deps += [
        "os_account:domain_account_innerkits",
        "os_account:os_account_innerkits",
      ]
      defines += [ "OS_ACCOUNT_EDM_ENABLE" ]
    }

    if (drivers_interface_usb_edm_enable && storage_service_edm_enable &&
        usb_manager_edm_enable) {
      external_deps += [ "storage_service:storage_manager_sa_proxy" ]
      sources += [ "./src/usb_read_only_plugin.cpp" ]
      defines += [ "USB_STORAGE_SERVICE_EDM_ENABLE" ]
    }

    if (storage_service_edm_enable) {
      external_deps += [ "storage_service:storage_manager_sa_proxy" ]
      sources += [ "./src/disallow_external_storage_card_plugin.cpp" ]
      defines += [ "EXTERNAL_STORAGE_SERVICE_EDM_ENABLE" ]
    }

    if (common_event_service_edm_enable) {
      external_deps += [ "common_event_service:cesfwk_innerkits" ]
      sources += [
        "./src/managed_browser_policy_plugin.cpp",
        "./src/set_browser_policies_plugin.cpp",
        "./src/utils/managed_browser_policy_serializer.cpp",
      ]
      defines += [ "COMMON_EVENT_SERVICE_EDM_ENABLE" ]
    }

    if (!enterprise_device_management_feature_pc_only) {
      sources += [
        "./src/disallowed_mms_plugin.cpp",
        "./src/disallowed_sms_plugin.cpp",
      ]
    }
  }

  deps = [
    "../../common/external:edm_external_adapters",
    "../../common/native:edm_commom",
    "../../interfaces/inner_api:edmservice_kits",
    "../../interfaces/inner_api/plugin_kits:plugin_kits",
  ]

  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
  }
  relative_install_dir = "edm_plugin"
  branch_protector_ret = "pac_ret"
  subsystem_name = "customization"
  part_name = "enterprise_device_management"
}

ohos_shared_library("sys_service_plugin") {
  sources = []
  public_configs = [
    ":edm_plugin_config",
    "../../common/config:coverage_flags",
  ]

  defines = []

  if (target_cpu == "arm64") {
    defines += [ "_ARM64_" ]
    print("defines _ARM64_")
  }

  if (target_cpu == "x86_64") {
    defines += [ "_X86_64_" ]
    print("defines _X86_64_")
  }

  external_deps = [
    "ability_base:want",
    "ability_runtime:app_manager",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "bundle_framework:appexecfwk_core",
    "cJSON:cjson",
    "c_utils:utils",
    "file_api:securitylabel",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
    "telephony_data:tel_telephony_data",
    "window_manager:libwm",
    "window_manager:sms",
  ]

  if (enterprise_device_management_support_all) {
    sources += [
      "./src/clear_up_application_data_plugin.cpp",
      "./src/utils/clear_up_application_data_param_serializer.cpp",
    ]
    defines += [ "EDM_SUPPORT_ALL_ENABLE" ]
    if (update_service_edm_enable) {
      external_deps += [ "update_service:updateservicekits" ]
      sources += [ "./src/reset_factory_plugin.cpp" ]
    }

    if (ability_runtime_edm_enable) {
      external_deps += [ "ability_runtime:ability_manager" ]
      sources += [
        "./src/disallowed_running_bundles_plugin.cpp",
        "./src/manage_auto_start_apps_plugin.cpp",
        "./src/manage_keep_alive_apps_plugin.cpp",
        "./src/utils/manage_keep_alive_apps_info.cpp",
        "./src/utils/manage_auto_start_app_info.cpp",
        "./src/utils/manage_auto_start_apps_serializer.cpp",
        "./src/utils/manage_keep_alive_apps_serializer.cpp",
      ]
    }

    if (certificate_manager_edm_enable) {
      external_deps += [ "certificate_manager:cert_manager_sdk" ]
      sources += [ "./src/user_cert_plugin.cpp" ]
    }

    if (pasteboard_edm_enable) {
      external_deps += [ "pasteboard:pasteboard_client" ]
      defines += [ "PASTEBOARD_EDM_ENABLE" ]
      sources += [ "./src/clipboard_policy_plugin.cpp" ]
    }

    sources += [
      "./src/set_permission_managed_state_plugin.cpp",
      "./src/utils/permission_managed_state_serializer.cpp"
    ]

    if (useriam_edm_enable) {
      external_deps += [ "user_auth_framework:userauth_client" ]
      defines += [ "USERIAM_EDM_ENABLE" ]
      sources += [
        "./src/fingerprint_auth_plugin.cpp",
        "./src/password_policy_plugin.cpp",
      ]
    }

    if (location_edm_enable) {
      external_deps += [ "location:locator_sdk" ]
      sources += [ "./src/location_policy_plugin.cpp" ]
    }

    if (time_service_edm_enable) {
      external_deps += [ "time_service:time_client" ]
      sources += [ "./src/set_datetime_plugin.cpp" ]
    }

    if (notification_edm_enable) {
      sources += [ "./src/disallowed_notification_plugin.cpp" ]
    }
  }
  deps = [
    "../../common/external:edm_external_adapters",
    "../../common/native:edm_commom",
    "../../interfaces/inner_api:edmservice_kits",
    "../../interfaces/inner_api/plugin_kits:plugin_kits",
  ]

  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
  }
  relative_install_dir = "edm_plugin"
  branch_protector_ret = "pac_ret"
  subsystem_name = "customization"
  part_name = "enterprise_device_management"
}

ohos_shared_library("need_extra_plugin") {
  sources = [
    "./src/utils/operate_device_param_serializer.cpp",
    "./src/utils/uninstall_param_serializer.cpp",
    "./src/utils/update_policy_serializer.cpp",
    "./src/utils/upgrade_package_info_serializer.cpp",
  ]
  public_configs = [
    ":edm_plugin_config",
    "../../common/config:coverage_flags",
  ]

  defines = []

  if (target_cpu == "arm64") {
    defines += [ "_ARM64_" ]
    print("defines _ARM64_")
  }

  if (target_cpu == "x86_64") {
    defines += [ "_X86_64_" ]
    print("defines _X86_64_")
  }

  external_deps = [
    "ability_base:want",
    "ability_runtime:app_manager",
    "access_token:libaccesstoken_sdk",
    "access_token:libtokenid_sdk",
    "cJSON:cjson",
    "c_utils:utils",
    "hilog:libhilog",
    "init:libbegetutil",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
    "window_manager:libwm",
    "window_manager:sms",
  ]

  if (enterprise_device_management_support_all) {
    defines += [ "EDM_SUPPORT_ALL_ENABLE" ]
    sources += [
      "./src/get_adminprovision_info_plugin.cpp",
      "./src/get_device_info_plugin.cpp",
      "./src/notify_update_packages_plugin.cpp",
      "./src/set_update_policy_plugin.cpp",
      "./src/set_wall_paper_plugin.cpp",
    ]
  }

  if (power_manager_edm_enable && screenlock_mgr_edm_enable &&
      update_service_edm_enable) {
    external_deps += [
      "power_manager:powermgr_client",
      "screenlock_mgr:screenlock_client",
      "update_service:updateservicekits",
    ]
    sources += [ "./src/operate_device_plugin.cpp" ]
  }

  if (telephony_core_edm_enable) {
    external_deps += [ "core_service:tel_core_service_api" ]
    defines += [ "TELEPHONY_CORE_EDM_ENABLE" ]
  }

  deps = [
    "../../common/external:edm_external_adapters",
    "../../common/native:edm_commom",
    "../../interfaces/inner_api:edmservice_kits",
    "../../interfaces/inner_api/plugin_kits:plugin_kits",
  ]

  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
  }
  relative_install_dir = "edm_plugin"
  branch_protector_ret = "pac_ret"
  subsystem_name = "customization"
  part_name = "enterprise_device_management"
}
